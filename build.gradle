/**
 * This is the main gradle build file for the Azure DevOps IntelliJ Plugin.
 */

plugins {
    id 'org.jetbrains.intellij' version '0.4.1' apply false
}

/**
 *This is task for update Gradle wrapper version.
 */
task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

/**
 * settings common to ALL projects (even the root level project)
 */
allprojects {
    configurations {
        codeAnalysisLibs
    }

    repositories {
        mavenCentral()
        maven {
            url "http://artifacts.eastus.cloudapp.azure.com:8081/nexus/content/repositories/snapshots"
        }
        maven {
            url "http://artifacts.eastus.cloudapp.azure.com:8081/nexus/content/repositories/releases"
        }
    }
}

/**
 * settings shared by each subproject (part 1)
 */
subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'findbugs'


    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task unitTest(type: Test) {
        exclude '**/L2/**'
    }
    
    sourceSets {
        main {
            compileClasspath += configurations.codeAnalysisLibs

            java {
                srcDir 'src'
            }

            resources {
                srcDir 'resources'
            }
        }

        test {
            compileClasspath += configurations.codeAnalysisLibs
            runtimeClasspath += configurations.codeAnalysisLibs

            java {
                srcDir 'test'
            }
        }
    }

    test {
        forkEvery = 10
    }
}

/**
 * specific settings for each project
 */
project(":plugin") {
    apply plugin: 'org.jetbrains.intellij'

    version buildNumber

    intellij {
        version = idea_version
        plugins = ['git4idea']
        pluginName = 'com.microsoft.vso.idea'
        updateSinceUntilBuild = false
    }

    patchPluginXml {
        pluginXmlFiles = ['META-INF/plugin.xml']
    }

    jar {
        baseName 'com.microsoft.alm.plugin.idea'
    }

    task showVersion() {
        println("version = " + buildNumber)
    }

    task zip(dependsOn: ['buildPlugin','test']) {}
}

project(":L2Tests") {
    apply plugin: 'org.jetbrains.intellij'

    intellij {
        version = idea_version
        plugins = ['git4idea']
    }

    dependencies {
        compile project(':plugin')
        testCompile project(':plugin').sourceSets.test.output
    }

    jar {
        baseName 'com.microsoft.alm.L2'
    }

    test {
        forkEvery = 1
    }
}


/**
 * settings shared by each subproject (part 2)
 */
subprojects {
    dependencies {
        codeAnalysisLibs 'com.google.code.findbugs:annotations:3.0.0'
        compile group: 'com.microsoft.alm', name: 'alm-http-client-dep', version: '0.4.3-SNAPSHOT'
        compile group: 'commons-io', name: 'commons-io', version: '2.4'
        compile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.28'
        compile group: 'org.glassfish.jersey.connectors', name: 'jersey-apache-connector', version: '2.28'
        compile group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: '2.28'
        compile (group: 'org.apache.httpcomponents', name: 'httpclient-win', version: '4.4.1') {
            exclude group: 'net.java.dev.jna', module: 'jna'
            exclude group: 'net.java.dev.jna', module: 'jna-platform'
        }

        compile (group: 'com.microsoft.alm', name: 'auth-core', version: '0.6.4') {
            exclude group: 'org.slf4j', module: 'slf4j-api'
        }

        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
        testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.6.3'
        testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: '1.6.3'
        testCompile group: 'org.powermock', name: 'powermock-classloading-xstream', version: '1.6.3'
        testCompile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    tasks.withType(FindBugs) {
        ignoreFailures = true
        reports {
            html { enabled = true }
            xml.enabled = !html.enabled
        }
    }

    /**
     * Preparing for release build
     */
    task prepRelease() {
    }

    /**
     * Open source prep
     */
    checkstyle {
        toolVersion = "6.1"
        ignoreFailures = false
        configFile = file "${rootDir}/config/checkstyle/custom-rules.xml"
        configProperties = [
            'checkstyle.java.header': "${rootDir}/config/checkstyle/java.header"
        ]
    }

    pmd {
        toolVersion = "5.0.3"
        ignoreFailures = true
        ruleSetFiles = files "${rootDir}/config/pmd/custom-pmd-rules.xml"
    }

    /**
     * Static code analysis tools
     */
    findbugs {
      toolVersion = "3.0.0"
    }
}
