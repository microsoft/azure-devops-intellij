// Copyright (c) Microsoft. All rights reserved.
// Licensed under the MIT license. See License.txt in the project root.

plugins {
    id "application"
    id "org.jetbrains.kotlin.jvm"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile project("tfs-sdk")
    compile "com.jetbrains.rd:rd-framework:$rdGenVersion"
}

compileKotlin {
    dependsOn ':client:protocol:rdgen'
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

mainClassName = "com.microsoft.tfs.MainKt"

evaluationDependsOnChildren() // to get properties of tfs-sdk subproject

task docs(type: Copy, dependsOn: ["tfs-sdk:downloadArchive", "tfs-sdk:verifyArchive"]) {
    def docs = file("$buildDir/docs")

    def tfsSdk = project("tfs-sdk")
    def sdkVersion = tfsSdk.ext.sdkVersion
    def sdkArchiveLocation = tfsSdk.ext.sdkArchiveLocation
    from(zipTree(sdkArchiveLocation)) {
        include "TFS-SDK-$sdkVersion/*.html"
        include "TFS-SDK-$sdkVersion/redist/*.*"
        eachFile { fcd ->
            fcd.relativePath = new RelativePath(true, fcd.relativePath.segments.drop(fcd.relativePath.segments.size() - 1))
        }
        includeEmptyDirs = false
    }
    into docs
}

distributions {
    main {
        contents {
            from(docs)
        }
    }
}