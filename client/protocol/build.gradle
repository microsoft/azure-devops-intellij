// Copyright (c) Microsoft. All rights reserved.
// Licensed under the MIT license. See License.txt in the project root.
buildscript {
    repositories {
        maven {
            url "https://cache-redirector.jetbrains.com/www.myget.org/F/rd-snapshots/maven"
        }
    }
    dependencies { classpath "com.jetbrains.rd:rd-gen:$rdGenVersion" }
}

plugins {
    id "org.jetbrains.kotlin.jvm"
    id "com.jetbrains.rdgen"
}

dependencies {
    compile "com.jetbrains.rd:rd-gen:$rdGenVersion"
}

def hashDir = "$buildDir/rdgen/hashes"
def backendGenerated = "$projectDir/../backend/src/main/kotlin/generated"
def connectorGenerated = "$projectDir/../connector/src/main/kotlin/generated"

clean {
    delete hashDir
    delete backendGenerated
    delete connectorGenerated
}

rdgen {
    verbose = true
    sources "$projectDir/src/main/kotlin/model"
    hashFolder = hashDir
    packages = "model.tfs"

    generator {
        language = "kotlin"
        transform = "reversed"
        root = "model.tfs.TfsModel"
        namespace = "com.microsoft.tfs.model.host"
        directory = backendGenerated
    }

    generator {
        language = "kotlin"
        transform = "asis"
        root = "model.tfs.TfsModel"
        namespace = "com.microsoft.tfs.model.connector"
        directory = connectorGenerated
    }
}